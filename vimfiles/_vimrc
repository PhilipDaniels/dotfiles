" To reload file while editing it, use :so %   (source current file)

set nocompatible    " Don't care about being compatible with vi.
set number          " Turn on line numbers
set vb t_vb=        " Turn off bleeping and flashing

" Ensure tabs are always 4 spaces.
set tabstop=4
set shiftwidth=4
set expandtab

" Turn off backups
set nobackup
set nowb
set noswapfile

" Set a status line.
set noruler
set laststatus=2
set statusline=
set statusline+=%-3.3n\                      " buffer number
set statusline+=%f\                          " file name
set statusline+=%{FileSizeForStatusLine()}\,  " file size
set statusline+=%L\ Lines\ 
set statusline+=%h%m%r%w                     " flags
set statusline+=[%{strlen(&ft)?&ft:'none'},  " filetype
set statusline+=%{strlen(&fenc)?&fenc:&enc}, " encoding
set statusline+=%{&fileformat}]\             " file format
set statusline+=%=                           " right align
"set statusline+=%{synIDattr(synID(line('.'),col('.'),1),'name')}\  " highlight
set statusline+=%b,0x%-8B\                   " current char
set statusline+=L%l,C%c\ [%P]
"set titlestring=%f\ %{FileSizeForStatusLine()}

" Turn on syntax highlighting and indentation based on filetype.
syntax on
filetype plugin indent on

" And ensure markdown files are edited as such, not Modula-2.
au BufRead,BufNewFile *.md set filetype=markdown


" ************** START COLOR SETUP **************  
" n.b gVim "just works". Most of this palaver is for console vim.
" See also: http://vim.wikia.com/wiki/256_colors_setup_for_console_Vim
" If all this isn't working, the "CSApprox" plugin may be an alternative,
" see http://www.vim.org/scripts/script.php?script_id=2390
" (the terminal must support 256 colors)
"
" Vim interrogation
" :set t_Co? - display how many colors Vim thinks the terminal supports
" :set term? - display the terminal type


" This preps solarized to run in dark mode.
set background=dark

" This is set to 16 by default, meaning that Solarized will attempt to use the
" standard 16 colors of your terminal emulator. You will need to set those
" colors to the correct Solarized values either manually or by importing one
" of the many colorscheme available for popular terminal emulators and
" Xdefaults. If you cannot install the palette, change this to 256 which
" will instruct solarized to use the 'degraded' 256 color scheme n.b. You
" need a 256-color capable terminal to do that!
let g_solarized_termcolors=16

" Without this, links in the built-in help are illegible blue blocks.
let g:solarized_underline=0

" Not found a reason to suppress these yet.
"let g:solarized_bold=0
"let g_solarized_italic=0

" Turn on trailing whitespace display?
"let g:solarized_visibility=normal


" Calling the color theme sets colors not just for code syntax but
" for many other things such as cursors and status lines...
colors solarized

" ...so if you want to override them you have to do it after the color
" theme is installed. "hi visual" determines how text is highlighted
" in visual mode, it is difficult to see in console Vim without this.
" The operative terms are ctermbg and ctermfg, the numbers after them
" are color palette indexes (they exactly correspond to what you can
" see in ConEmu's colors dialog page). Do ":verbose hi visual" to
" determine where this is set. See ":help highlight" for details on how
" to set highlights and ":help highlight-groups" for a list of the
" things that you can highlight.
highlight Visual term=reverse cterm=none ctermbg=13 ctermfg=15 guibg=#6c71c4 guifg=#fdf6e3
highlight StatusLine ctermbg=7 ctermfg=4 guibg=#eee8d5 guifg=#268bd2
highlight StatusLineNC ctermbg=4 ctermfg=7 guibg=#268bd2 guifg=#eee8d5

" The first line turns on highlighting of the current line, but the second
" line then cancels the highlighting effect by setting it to none. The third
" line highlights the line number in the gutter column at the left; it needs
" the first line to work!
set cursorline
highlight CursorLine ctermbg=none
highlight CursorLineNr term=bold ctermfg=4 gui=bold guifg=#268bd2

" You can also highlight the current column in the same way.
"set cursorcolumn

" Allow quick toggle between light and dark solarized.
call togglebg#map("<F12>")

" This is used to highlight long lines, it draws a subdued (in solarized)
" vertical line at the 80th column.
set colorcolumn=80

" ************** END COLOR SETUP **************  


" Turn on automatic indentation. The Wiki says only do this manually if
" filetype indentation is not working.
"set smartindent

" Backspace and cursor keys wrap to previous/next line and make
" Backspace work correctly.
set backspace=indent,eol,start whichwrap+=<,>,[,]

" Backspace in Visual mode deletes selection
vnoremap <BS> d

" Buffer and tab navigation.
map <silent> <C-Tab> :bnext<CR>
map <silent> <C-S-Tab> :bprevious<CR>
map <silent> <C-J> :bnext<CR>
map <silent> <C-K> :bprev<CR>
map <silent> <C-L> :tabn<CR>
map <silent> <C-H> :tabp<CR>

" Make F2 and F3 run the macros stored in the 'q' and 'w' registers
" and Shift F2/F3 run the macros until a blank line is encountered.
map <F2> @q
map <F3> @w
map <silent> <S-F2> :call RunMacroToBlankLine('q')<CR>
map <silent> <S-F3> :call RunMacroToBlankLine('w')<CR>

" Window resizing on function keys, +/- and the numeric keypad.
map <F5> <C-W><
map <F6> <C-W>>
map <F7> <C-W>-
map <F8> <C-W>+

nnoremap <silent> _ :resize -1<CR>
nnoremap <silent> + :resize +1<CR>

" Quick access to NERDTree.
map <C-n> :NERDTreeToggle<CR>

" ********** Function Definitions **********
function! StripString(input_string)
    " Remove leading and trailing whitespace from input_string.
    return substitute(a:input_string, '^\s*\(.\{-}\)\s*$', '\1', '')
endfunction

function! RunMacroToBlankLine(registerName)
    " Repeatedly run the macro stored in the registerName until the
    " end of file or a blank line is reached. For this to work as you
    " expect your macro should move down one line as part of its
    " definition (typically it is the last thing it does).
    let curLine = line(".")
    let lastLine = line("$")
    for line in getline(curLine, lastLine)
        let sLine = StripString(line)
        if (strlen(sLine) == 0)
            break
        endif
        let cmd = '@' . a:registerName
        execute "normal " . cmd
    endfor
endfunction

function! FileSizeForStatusLine()
    " Return file size in a format suitable for the status line.
    let bytes = getfsize(expand("%:p"))
    if bytes <= 0
        return ""
    endif
    if bytes < 1024
        return bytes
    else
        return (bytes / 1024) . "K"
    endif
endfunction

